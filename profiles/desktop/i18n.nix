{
  config,
  lib,
  pkgs,
  user,
  ...
}:
let
  cfg = config.profiles.desktop.i18n;
in
{
  options.profiles.desktop.i18n = {
    enable = lib.mkEnableOption "i18n" // {
      default = config.profiles.desktop.enable;
    };
  };

  config = lib.mkIf cfg.enable {
    # to use svg themes
    programs.gdk-pixbuf.modulePackages = [ pkgs.librsvg ];

    i18n = {
      defaultLocale = "en_US.UTF-8";
      supportedLocales = [
        "zh_CN.UTF-8/UTF-8"
        "en_US.UTF-8/UTF-8"
      ];
      inputMethod = {
        enable = true;
        type = "fcitx5";
        fcitx5 = {
          addons = with pkgs; [
            fcitx5-mellow-themes
            (fcitx5-rime.override {
              rimeDataPkgs = [
                rime-data
                rime-ice
                rime-zhwiki
                rime-moegirl
              ];
            })
            fcitx5-chinese-addons
          ];
          plasma6Support = true;
          waylandFrontend = true;
        };
      };
    };

    home-manager.users.${user} = {
      home.sessionVariables = lib.mkMerge [
        {
          LANG = "zh_CN.UTF-8";
          XMODIFIERS = "@im=fcitx";
        }
        (lib.optionalAttrs (!config.services.desktopManager.plasma6.enable) {
          GTK_IM_MODULE = "wayland"; # `wayland` to use tiv3, `fcitx` to use fcitx im module
          QT_IM_MODULE = "fcitx";
          QT_IM_MODULES = "wayland;fcitx;ibus";
        })
      ];

      systemd.user.services = {
        "fcitx5" = {
          Install = {
            WantedBy = [ "graphical-session.target" ];
          };
          Unit = {
            PartOf = [ "graphical-session.target" ];
            After = [
              "graphical-session.target"
              "xwayland-satellite.service"
            ];
          };
          Service = {
            ExecStart = "${lib.getExe' config.i18n.inputMethod.package "fcitx5"} --replace";
            Restart = "on-failure";
          };
        };
      };

      # TODO: refactor
      # https://github.com/xddxdd/nixos-config/blob/a2b7311f03e9dc121f0b3fc844b31068fa076066/nixos/client-apps/fcitx/rime-lantian-custom.nix

      xdg = {
        # entries generated by xdg.autostart will have a store path prefix in filename, preventing us from overwrite the system level entry
        configFile."autostart/org.fcitx.Fcitx5.desktop".text = ''
          [Desktop Entry]
          Type=Application
          Name=Fcitx 5
          Hidden=true
        '';
      };
    };
  };
}
